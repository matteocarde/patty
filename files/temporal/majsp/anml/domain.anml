type Robot;
type Pallet;
type Position;
type Treatment;

fluent boolean robot_at(Robot r, Position p);
fluent boolean robot_has(Robot r, Pallet b);
fluent boolean pallet_at(Pallet b, Position p);
fluent boolean robot_free(Robot r);
fluent boolean position_free(Position p);
fluent boolean treated(Pallet b, Treatment t);
fluent boolean ready(Pallet b, Position p, Treatment t);
fluent integer[0, 50] battery_level(Robot r);

constant boolean can_do(Position p, Treatment t);
constant boolean is_depot(Position p);
constant integer distance(Position from, Position to);

action move(Robot r, Position from, Position to) {
   duration := 1;
   [start] not (from == to);
   [start] robot_at(r, from);
   [start] battery_level(r) >= distance(from, to);
   [start] robot_at(r, from) := false;
   [start] robot_at(r, to) := true;
   [start] battery_level(r) := battery_level(r) - distance(from, to);
};

action unload_at_depot(Robot r, Pallet b, Position p) {
   duration := 1;
   [start] is_depot(p);
   [start] robot_has(r, b);
   [start] robot_at(r, p);
   [start] pallet_at(b, p) := true;
   [start] robot_free(r) := true;
   [start] robot_has(r, b) := false;
};

action load_at_depot(Robot r, Pallet b, Position p) {
   duration := 1;
   [start] is_depot(p);
   [start] robot_free(r);
   [start] robot_at(r, p);
   [start] pallet_at(b, p);
   [start] robot_free(r) := false;
   [start] robot_has(r, b) := true;
   [start] pallet_at(b, p) := false;
};

action make_treatment(Robot r, Pallet b, Position p, Treatment t) {
   duration := 2;
   [start] can_do(p, t);
   [start] position_free(p);
   [start] robot_at(r, p);
   [start] robot_has(r, b);
   [start] not treated(b, t);
   [start] position_free(p) := false;
   [start] robot_has(r, b) := false;
   [start] pallet_at(b, p) := true;
   [start] robot_free(r) := true;
   [start + 1] ready(b, p, t) := true;
   [end] treated(b, t);
   [end] position_free(p);
};

action load(Robot r, Pallet b, Position p, Treatment t) {
   duration := 0.5;
   [start] pallet_at(b, p);
   [start] robot_free(r);
   [start] ready(b, p, t);
   [start] robot_at(r, p);
   [start] robot_free(r) := false;
   [start] ready(b, p, t) := false;
   [start] pallet_at(b, p) := false;
   [start] robot_has(r, b) := true;
   [start] treated(b, t) := true;
   [start] position_free(p) := true;
};

::("metric", "minimize-makespan");
